#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4;-*-

# Copyright (c) 2023 Sebastian Gniazdowski

int/iq::parse-opts() {
    # Preamble – emulate & 0=…
    SNIP_EMULATE_OPTIONS_ZERO
    tmp/iq::prune-dashes() {
        # Remove any --/- end option mark
        integer QIDX=${@[(i)(--|-)]}
        ((QIDX<=$#))&&reply=("$@[1,QIDX-1]" "$@[QIDX+1,-1]")||reply=("$@")
    }

    local OPTSPEC=$1 ASVAR=$2 ARVAR=$3
    shift 3
    tmp/iq::prune-dashes "$@"
    local -a qe=("$reply[@]")

    builtin zparseopts SNIP_-F_OPT_FOR_ZP_OPTS_VERSION \
        -D -E ${=ASVAR:+-A $ASVAR} ${=ARVAR:+-a $ARVAR} -- \
            ${(s: :)OPTSPEC} ||return 7 #zsweep:pass

    # Save remains without options in reply (pruning --/-)
    tmp/iq::prune-dashes "$@"

    # Get the recognized options
    REPLY="${(j| |)${(@)qe:|reply}}"
    return 0
 }