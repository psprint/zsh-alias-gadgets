#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2023 Sebastian Gniazdowski

# Run as script? Or as a function? Supports both ways.

iqopen()
{
# Preamble – emulate & 0=…
eval "${SNIP_EMULATE_OPTIONS_ZERO:-false}"||\
    0=${${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}:A}

source $0:h:h/share/preamble.inc.zsh --script --fun --cleanup

local -A Opts;local -a opts
int/iq::init-sm-con Opts opts "$@"||{print QE>>/tmp/reply;return 13}
set -- "$reply[@]"
eval "$REPLY"

iqw(){
    -zui_glib_msg "$@"
}

iqopen_util_swap4(){
    local Q=$1 Q0=$2
    shift 0+2
    local -a vars=("$@")

    : ${(P)vars[Q0+1]::=${(P)vars[4]}}
    : ${(P)vars[4]::=${(P)vars[Q+1]}}
}

iqopen_util_set_current_idx(){
    integer IN=ZUI[PREV_CURRENT_IDX]
    local QPATH=${ZUI[QPATH]:+_$ZUI[QPATH]}
    # Tree
    if ((ZUI[GEN_B_IDX]==0));then
       ((ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_PVIEW_BKP$QPATH]||\
         ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_GIT_BKP]))&&\
            ZUI[PREV_CURRENT_IDX]=${ZUI[CURRENT_IDX_MAIN_BKP]:-5}
    # Preview
    elif ((ZUI[GEN_B_IDX]==1));then
        ((ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_MAIN_BKP]||\
          ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_GIT_BKP]))&&\
            ZUI[PREV_CURRENT_IDX]=${${ZUI[CURRENT_IDX_PVIEW_BKP$QPATH]:#0#}:-5}
    elif ((ZUI[GEN_B_IDX]==0+2));then
        ((ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_PVIEW_BKP$QPATH]||\
          ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_MAIN_BKP]))&&\
            ZUI[PREV_CURRENT_IDX]=${${ZUI[CURRENT_IDX_GIT_BKP]:#0#}:-5}
    fi
    if ((IN!=ZUI[PREV_CURRENT_IDX]));then
        ZUI[CURRENT_IDX]=$ZUI[PREV_CURRENT_IDX]
        -zui_list_verify_vars
        ZUI[redraw]=1
    fi
    if ((!ZUI[UNIQ_MODE]&&!$#ZUI[SEARCH_BUFFER]&&\
            ZUI[CURRENT_IDX]<=${${(On)ZUILIST_NONSELECTABLE_ELEMENTS}[1]}));then
        ZUI[CURRENT_IDX]=${${(On)ZUILIST_NONSELECTABLE_ELEMENTS}[1]}
        ZUI[redraw]=1
    fi
}

iqopen_cmd_synh_switch(){
    ((ZUI[SRC_HIGHLIGHT]=1-ZUI[SRC_HIGHLIGHT]))
    ZUI[SRC_HIGHLIGHT_SWITCHED]=1
    if iqopen_hub;then
        ((ZUI[SRC_HIGHLIGHT]))&&local txt=" ON"||local txt=" OFF"
        iqw "Syntax highlighting switched" $txt
    fi
}

iqopen_cmd_next(){
    integer q=${1##<->/} qq=${1%%/<->} in=$ZUI[GEN_B_IDX]

    # Three kind of args, first: num0/num1
    if ((q!=qq));then
        ((ZUI[GEN_B_IDX]==q))&&ZUI[GEN_B_IDX]=$qq||\
            {((ZUI[GEN_B_IDX]==qq))&&ZUI[GEN_B_IDX]=$q||ZUI[GEN_B_IDX]=0;}
    # Second: num
    elif (($#1));then
        ZUI[GEN_B_IDX]=$q
    # Third: no-arg
    else
        ((ZUI[GEN_B_IDX]=(1+ZUI[GEN_B_IDX])%3))
    fi
    iqopen_view_switch $in $ZUI[GEN_B_IDX]
    iqopen_hub
}

iqopen_view_switch(){
    integer in=$2 in0=$1
    ZUI[VIEW_CHANGE]=1
    local QPATH=${ZUI[QPATH]:+_$ZUI[QPATH]} QTXT

    noglob iqopen_util_swap4 $in $in0 \
        ZUI[SEARCH_BUFFER_GLOBAL] \
        ZUI[SEARCH_BUFFER_PREVIEW]\
        ZUI[SEARCH_BUFFER_GIT]\
        ZUI[SEARCH_BUFFER]

    local -a qt=(·preview·\  ·file·\  ·git·\ )
    ((ZUI[IN_SEARCH]))&&iqw "Restored previous " $qt[ZUI[GEN_B_IDX]+1] "search"

    buffer=$ZUI[SEARCH_BUFFER]
    if ((in0==1));then
        ZUI[CURRENT_IDX_PVIEW_BKP$QPATH]=$ZUI[CURRENT_IDX]
    elif ((in0==2));then
        ZUI[CURRENT_IDX_GIT_BKP]=$ZUI[CURRENT_IDX]
    elif ((in0==0));then
        ZUI[CURRENT_IDX_MAIN_BKP]=$ZUI[CURRENT_IDX]
    fi
    ZUI[PREV_CURRENT_IDX]=$ZUI[CURRENT_IDX]
}

separ() {
    REPLY="$ZUI[YELLOW]${(l:COLUMNS*3::—:):-}$ZUI[FMT_END]";
}

if ((!$+Opts[--refresh])); then
    ok/qnstr $PWD&&local APP=$REPLY
    ZUI[SRC_HIGHLIGHT]=1
    -zui_glib_cleanup deserialize:"iqopen_$APP"
    -zui_glib_init app:"iqopen_$APP" app_name:"°Open° Console"
    emulate -L zsh -o extendedglob -o typesetsilent -o warncreateglobal -o combiningchars -o rcquotes
    -zui_glib_init2 # after emulate
    ZUI[GEN_B_IDX]=0 ZUI[GIT_DIFF]=0

    # Needed for immediate output from coprocesses (disables buffering).
    ((${+commands[stdbuf]}))&& \
        local -a stdbuf1=( stdbuf -o0 -i0 ) stdbuf2=( stdbuf -o0 -i0 )
    ((${+commands[gstdbuf]}))&& \
        local -a stdbuf1=( gstdbuf -o0 -i0 ) stdbuf2=( gstdbuf -o0 -i0 )

    ZUI[COLORING_MATCH_MULTIPLE]=1
    ZUI[COLORING_COLOR]=$ZUI[BOLD]$ZUI[YELLOW]

    -zui_glib_store_default_app_config b:instant_search 1
    -zui_glib_store_default_app_config s:select_mode "quit"   # Quit event loop on text-selection
    -zui_glib_store_default_app_config b:text_bar "[F1 jump/srch res.][F2 preview][F3 diff][F4 Tig][F5 git commit]"
    -zui_glib_store_default_app_config b:status_size 5
    -zui_glib_store_default_app_config b:status_pointer 1
    -zui_glib_store_default_app_config b:text_select 1        # Allow selection of text
    -zui_glib_store_default_app_config s:timeout 25
    -zui_glib_store_default_app_config s:mark "white MAGENTA reverse"
    -zui_glib_store_default_app_config s:text_mode off
    -zui_glib_store_default_app_config b:top_anchors 0
    ZUI[UNIQ_MODE]=0
    [[ $1 == open ]]&&shift
    [[ -n $* ]]&&ZUI[SEARCH_BUFFER]=$*
    ZUI[START_IN_SEARCH]=1

    local -a qitems iq0items q0input q0inputp
    integer Q0_SAVE_PIPE
    if read -k -t -u0; then
        ZUI[FIRST_0_INPUT]=$REPLY
        ZUI[HAVE_0_INPUT]=1
        ZUI[GEN_B_IDX]=0
        exec {Q0_SAVE_PIPE}<&0
    fi
fi

zui::bindkey F2 '{iqopen_cmd_next 0/1;}'
zui::bindkey F3 '{iqopen_cmd_next 2;}'
zui::bindkey F4 '{tput reset;command tig;zcurses end;zcurses refresh;ZUI[redraw]=1;}'
zui::bindkey F5 '{tput reset;command git commit;read -k 1</dev/tty;zcurses end;zcurses refresh;ZUI[redraw]=1;}'

#    zui::bindkey F3 "{print q:$ZUI[mark]}"
zui::bindkey F9 '{print view:$ZUI[SEARCH_BUFFER_PREVIEW], gl:$ZUI[SEARCH_BUFFER_GLOBAL], sbuff:$ZUI[SEARCH_BUFFER], current:$ZUI[CURRENT_IDX], PVIEW_Q:$ZUI[CURRENT_IDX_PVIEW_BKP_$ZUI[QPATH]], PVIEW:$ZUI[CURRENT_IDX_PVIEW_BKP], main:$ZUI[CURRENT_IDX_MAIN_BKP] sptr:$ZUI[status_pointer]|tee -a /tmp/reply;}'
zui::bindkey F10 '{builtin source $ZIQ_CON_DIR/iqopen --refresh||\
                    iqw -r "Reload finished with error: " $?;}'
zui::bindkey F7 '{iqopen_cmd_synh_switch;}'

iqopen_hub(){
    integer EC
    local -a gener=(iqopen_fill_view_tree iqopen_fill_view_preview iqopen_fill_or_set_view_git)
    $gener[1+(ZUI[GEN_B_IDX]%3)]
    EC=$?
    ((${3:-0}))||{-zui_glib_fly_array_refresh 1;}

    return EC
}

iqopen_fill_view_preview() {
    local QPATH QTXT
    ZUI[COLORING_PATTERN]="·[^·]##·" ZUI[SRC_HIGHLIGHT_SWITCHED]=0
    ZUI[COLORING_MATCH_MULTIPLE]=1
    ZUI[COLORING_COLOR]=$ZUI[BOLD]$ZUI[YELLOW]
    if ((ZUI[CURRENT_IDX]>0&&ZUI[SRC_HIGHLIGHT_SWITCHED]==0&&ZUI[GEN_B_IDX]==1));then
        QPATH=$__list[ZUI[CURRENT_IDX]]
        ZUI[QPATH]=$QPATH
    else
        QPATH=$ZUI[QPATH]
    fi
    {
        qitems=() iq0items=()
        if [[ -f $QPATH ]];then
            QTXT="Contents of file: ·$QPATH·"\
" located at: ·${${${(M)QPATH##/*}:+$QPATH:h}:-$PWD}/·"
            ((ZUI[SRC_HIGHLIGHT]))||return 0
            int/iq::get-file-type -s -q $QPATH||\
                {iqw "Unknown file type, no syntax highlighting";throw NoTool}
            int/iq::get-stx-cmd -q $REPLY||\
                {iqw "Error: no highlighter command found (e.g.: bat, highlight)"
                throw NoType;}
            local -a qcmd=("$reply[@]" "$QPATH")
            iq0items=("${(@f)$($qcmd[@])}")
        else
            QTXT="!NO FILE SELECTED, PRESS F2 TO SHOW FILE TREE!"
        fi
    } always {
        if catch \* || [[ $iq0items == $~galiases[WRONGSTR] ]];then
            [[ -f $QPATH ]]&&iq0items=("${(@f)$(<$QPATH)}")
        fi
        if [[ $iq0items != $~galiases[WRONGSTR] ]];then
            qitems=("$iq0items[@]")
        else
            qitems=("No such file")
        fi

        reply=()
        -zui_glib_special_text $QTXT
        separ
        typeset -ga -U ZUILIST_NONSELECTABLE_ELEMENTS=(4 3 2 1)
        qitems=("$REPLY" "$reply[1]" "$REPLY" "$qitems[@]")
    }
}

iqopen_fill_view_tree() {
    ZUI[COLORING_PATTERN]="(((([^\/—])(#c2,))(#e))|·[^·]##·)"
    ZUI[COLORING_MATCH_MULTIPLE]=1
    ZUI[COLORING_COLOR]=$ZUI[BOLD]$ZUI[YELLOW]
    ZUI[QIDX]=0 ZUI[SRC_HIGHLIGHT_SWITCHED]=0
    reply=()
    if [[ -n $ANGEL_OPEN_DEFAULT_COMMAND ]];then
        -zui_glib_special_text "Output of cmd: ·$ANGEL_OPEN_DEFAULT_COMMAND·"
        iq0items=("${(@f)$(eval $=ANGEL_OPEN_DEFAULT_COMMAND)}")
    else
        -zui_glib_special_text "Showing files in: ·$PWD·"
        iq0items=(**/*~*~(N.,@))
    fi
    (($+Opts[-s]))&&qitems=("${(@on)qitems}")
    (($+Opts[-R]))&&qitems=("${(a@nO)qitems}")

    typeset -ga -U ZUILIST_NONSELECTABLE_ELEMENTS=(4 3 2 1)
    separ
    qitems=("$REPLY" "$reply[1]" "$REPLY" "$iq0items[@]")
}

iqopen_fill_or_set_view_git() {
    local col=$ZUI[WHITE]
    ZUI[COLORING_PATTERN]="(#s)[^+-]*" ZUI[COLORING_COLOR]=$col
    ZUI[COLORING_MATCH_MULTIPLE]=1
    ((!ZUI[GIT_DIFF]))&&iqw "Git diff detected"
    (($1))&&ZUI[GEN_B_IDX]=2
    ZUI[GIT_DIFF]=1

    if ((!ZUI[HAVE_0_INPUT]));then
        iq0items=("${(@f)$(command git diff --color=always)}")
        local -a qreply=()
        foreach QE ("$iq0items[@]")
            int/iq::prune-esc -b $QE
            qreply+=("$REPLY")
        end
        iq0items=("$qreply[@]")
    fi
    typeset -ga -U ZUILIST_NONSELECTABLE_ELEMENTS=(4 3 2 1)
    separ
    reply=()
    -zui_glib_special_text "Git diff output"
     qitems=("$REPLY" "$reply[1]" "$REPLY" "$iq0items[@]")
}

# Read & publish Configure/make output
-zui-standard-timeout-callback() {
    typeset -ga -U ZUILIST_NONSELECTABLE_ELEMENTS=(4 3 2 1)
    ((!ZUI[HAVE_0_INPUT]&&!ZUI[VIEW_CHANGE]))&&return

    if ((ZUI[HAVE_0_INPUT]));then
        int/iq::read-all -u$Q0_SAVE_PIPE -t0 -x100
        local ql had_read=0
        integer q0idx=$#q0input+1
        if ((!$#q0input));then
            q0input[q0idx]=$ZUI[FIRST_0_INPUT]$reply[1]
            shift reply
        fi
        local -a qreply=()
        foreach QE ("$reply[@]")
            int/iq::prune-esc -b $QE
            qreply+=("$REPLY")
        end
        q0input+=("$qreply[@]")
        q0inputp=("$q0input[@]")
        (($+Opts[-s]))&&q0inputp=("${(@on)q0inputp}")
        (($+Opts[-R]))&&q0inputp=("${(a@nO)q0inputp}")
        if ((ZUI[GEN_B_IDX]%3==0||ZUI[GEN_B_IDX]%3==2));then
            reply=()
            -zui_glib_special_text "·Below are entries read from input·"
            separ
            local -a q0items=("$qitems[@]")
            qitems=($REPLY "$reply[1]" $REPLY "$q0inputp[@]")
            [[ -n ${qitems:|q0items} ]] && -zui_glib_fly_array_refresh 1
        fi
    fi
    # Input is a diff?
    ((!ZUI[GIT_DIFF]))&&[[ -n ${(M)qitems[1,15]:#*diff[[:space:]]##--git*} ]]&&iqopen_fill_or_set_view_git 1

    ((ZUI[VIEW_CHANGE]))||return 0
    iqopen_util_set_current_idx
    ZUI[VIEW_CHANGE]=0
    return 0
}

if (($+Opts[--refresh])); then
    iqw "Reload went OK, status normal"
    return 0
fi

## Start application ##
-zui_glib_msg "" "@@@ " "Welcome to " "IQ System " "°open° console!" " @@@";
integer EC
iqopen_hub 1 1 1
while (( 1 )); do
    ZUI[VIEW_CHANGE]=1
    builtin print -rn -- $terminfo[smcup]$termcap[ti]
    zui-event-loop a:qitems
    builtin print -rn -- $terminfo[rmcup]$termcap[te]
    [[ -z $REPLY ]]&&break
    local QFILE= REPLY_SAVE=$REPLY; integer QIDX=0
    if [[ -z $ZUI[SEARCH_BUFFER] ]];then
         if ((ZUI[GEN_B_IDX]%3==1));then
            QFILE=$ZUI[QPATH]
            QIDX=ZUI[USER_CURRENT_IDX]
            iqw "Opening " $QFILE " scrolled to line: " "#$QIDX"
        elif ((ZUI[GEN_B_IDX]%3==2));then
            : ??? FIXME
        elif [[ $REPLY_SAVE != *(·|–|—)* ]];then
            QFILE=$REPLY_SAVE
        fi
    else
        repeat 1; do typeset +U -ga __slist=("$__qlist[@]");done
        if -zui_sys_get_match_line 'QIDX'; then
            if ((ZUI[GEN_B_IDX]%3==1));then
                QFILE=$ZUI[QPATH]
                iqw "Opening " $QFILE " scrolled to line: " "#$QIDX"
            elif ((ZUI[GEN_B_IDX]%3==2));then
                : ??? FIXME
            elif [[ $REPLY_SAVE != *(·|–|—)* ]];then
                QFILE=$REPLY_SAVE
                QIDX=0
            fi
        fi
    fi
    if [[ -n $QFILE ]];then
        REPLY=
        if [[ -n $ANGEL_OPEN_CMD ]];then
            REPLY=$ANGEL_OPEN_CMD
        elif ! int/iq::get-editor;then
            EC+=$?
            iqw "" "" "" 'ERROR: Couldn''t find editor, tried '\
                "\$VISUAL (=$VISUAL) " then "" " \$EDITOR (=$EDITOR)"
            ZUI[IN_SEARCH]=0
        fi
        [[ -n $REPLY ]]&&\
            (
                # Run the editor in $REPLY, first cd-ing into project's directory
                int/iq::run-command -C $PWD \
                    "$REPLY" ${${QIDX:#0##}:++$QIDX} -- $QFILE
            )
        EC+=$?
    fi
    zcurses clear main redraw
    zcurses clear status redraw
done

-zui_glib_cleanup serialize
builtin print -n $terminfo[rmcup]$termcap[te]
return 0
}
iqopen "$@"

# vim:ft=zsh:tw=80:sw=4:sts=4:et:foldmarker=[[[,]]]
