#!/usr/bin/env zsh

int/iq::get-prj-dir() {
    # Preamble – emulate & 0=…
    eval "${SNIP_EMULATE_OPTIONS_ZERO:-false}"||\
        0=${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}

    local -a opts; local -A Opts
    int/iq::parse-opts "q -quiet" Opts opts "$@"||return 7
    set -- "$reply[@]"
    int/iq::opt-cascade Opts -q --quiet
    int/iq::reset
    integer retval

    tmp/iq::up-find-dir() {
        local W=$1
        reply=($PWD/(../)#$~W(N.,@,/))
        reply=(${(@On)reply})
        # Fix path's ../,etc.
        REPLY=$reply[1]:a
        [[ -n $REPLY ]]
    }

    # Input/options
    local PRJ FILEDIR
    local -a qt=(.git TAGS tags CMakeLists.txt configure)
    # Iterate over special files/dirs
    for FILEDIR ($qt); do
        tmp/iq::up-find-dir $FILEDIR&&PRJ=$REPLY&&break
    done

    REPLY=$REPLY:h
    if [[ $PRJ == WRONGSTR ]]; then
        iqwarn -Q ${Opts[-q]+-q} $ZIQ[Q10_NO_PROJECT_DIR_FOUND]
        return 1
    fi
    return 0
}
