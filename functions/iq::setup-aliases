#!/usr/bin/env zsh

iq::setup-aliases() {

#
# Copyright (c) 2023 Sebastian Gniazdowski
#

# Set options
builtin emulate -L zsh -o extendedglob -o noshortloops \
                        -o warncreateglobal -o typesetsilent \
                        -o nopromptsubst

# Possibly fix $0 with a new trick â€“ use of a %x prompt expansion
0=${${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}:A}

# Input/options
local -A Opts
builtin zparseopts ${${(M)ZSH_VERSION:#(5.[8-9]|6.[0-9])}:+-F} \
            -D -E -A Opts -- p: -path: || return 1
(($+Opts[-p]))&&Opts[--path]=$Opts[-p]

if [[ ! -d $ZIQAES && ! -d $Opts[--path] ]];then
    print 'ZshSweep:' error: alias dir not set \(\$ZIQAES\)
    return 1
fi

local DIR dirs=($Opts[--path](/N) $ZIQAES $ZIQAES/*(/N) $ZIQTXT(/N))
for DIR ($dirs);do
    # Set up aliases (global, suffix and the proper ones)
    [[ -f $DIR/*[^~](#qNY1.,@) ]]&&for REPLY in $DIR/*[^~](N.,@);do
        if [[ $REPLY != $ZIQTXT/* ]];then
            REPLY="$REPLY:t=$(<$REPLY)"
            local KEY=${(M)REPLY##[^=]##}
            alias "${${REPLY#*=}%%:*}" \
                "${(M)REPLY##[^=]##}=${REPLY#*:}"
            ZIQ[$KEY]=${REPLY#*:}
            [[ $KEY == SNIP_* ]]&&typeset -g $KEY="${REPLY#*:}"
        else
            local QVAL=$REPLY:t
            ZIQ[$QVAL]="$(<$REPLY)"
            ZIQ[$QVAL]=${(%)ZIQ[$QVAL]}
        fi
    done
done
int/iq::reset

}
