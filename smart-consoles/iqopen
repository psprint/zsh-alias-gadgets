#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2023 Sebastian Gniazdowski

# Run as script? Or as a function? Supports both ways.

iqopen()
{
# Preamble – emulate & 0=…
eval "${SNIP_EMULATE_OPTIONS_ZERO:-false}"||\
    0=${${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}:a}
source $0:h:h/share/preamble.inc.zsh --script --fun

local -A Opts;local -a opts
int/iq::init-sm-con Opts opts "$@"||{print QE>>/tmp/reply;return 13}
set -- "$reply[@]"
eval "$REPLY"
ZUI[SEARCH_BUFFER]=$*

iqw(){
    -zui_glib_msg "$@"
}

iqopen_preview_file(){
    ((ZUI[gen_B_idx]=1-ZUI[gen_B_idx]))
    ZUI[VIEW_CHANGE]=1
    if ((ZUI[gen_B_idx]%2));then
        ZUI[CURRENT_IDX_MAIN_BKP]=$ZUI[CURRENT_IDX]
        #ZUI[CURRENT_IDX]=$ZUI[CURRENT_IDX_PVIEW_BKP]
    else
        ZUI[CURRENT_IDX_PVIEW_BKP]=$ZUI[CURRENT_IDX]
        #ZUI[CURRENT_IDX]=$ZUI[CURRENT_IDX_MAIN_BKP]
    fi
    ZUI[PREV_CURRENT_IDX]=$ZUI[CURRENT_IDX]
    -zui_glib_fly_mod_regen 2 1
    #__last_element=$#__list
}

separ() {
    REPLY="$ZUI[YELLOW]${(l:COLUMNS*3::—:):-}$ZUI[FMT_END]";
}

if ((!$+Opts[--refresh])); then
    ok/qnstr $PWD&&local APP=$REPLY
    -zui_glib_cleanup deserialize:"iqopen_$APP"
    -zui_glib_init app:"iqopen_$APP" app_name:"°Open° Console"
    emulate -L zsh -o extendedglob -o typesetsilent -o warncreateglobal -o combiningchars
    -zui_glib_init2 # after emulate -LR
    ZUI[gen_B_idx]=0

    # Needed for immediate output from coprocesses (disables buffering).
    ((${+commands[stdbuf]}))&& \
        local -a stdbuf1=( stdbuf -o0 -i0 ) stdbuf2=( stdbuf -o0 -i0 )
    ((${+commands[gstdbuf]}))&& \
        local -a stdbuf1=( gstdbuf -o0 -i0 ) stdbuf2=( gstdbuf -o0 -i0 )

    ZUI[COLORING_MATCH_MULTIPLE]=1
    ZUI[COLORING_COLOR]=$ZUI[BOLD]$ZUI[YELLOW]
    ZUI[START_IN_SEARCH_MODE]=1

    -zui_glib_store_default_app_config b:instant_search 1
    -zui_glib_store_default_app_config s:select_mode "quit"   # Quit event loop on text-selection
    -zui_glib_store_default_app_config b:text_select 1        # Allow selection of text
    -zui_glib_store_default_app_config s:timeout 25
    -zui_glib_store_default_app_config s:mark "white MAGENTA reverse"
    -zui_glib_store_default_app_config s:text_mode off
    -zui_glib_store_default_app_config b:top_anchors 0
    local -a qfiles qlines GEN2_HOPS
fi

zui::bindkey F2 {iqopen_preview_file}
#    zui::bindkey F3 "{print q:$ZUI[mark]}"
zui::bindkey F9 '{print current:$ZUI[CURRENT_IDX], PVIEW:$ZUI[CURRENT_IDX_PVIEW_BKP], main:$ZUI[CURRENT_IDX_MAIN_BKP];}'
zui::bindkey F10 '{builtin source $ZIQ_CON_DIR/iqopen --refresh||\
                    iqw -r "Reload finished with error: " $?;}'

generator_A() {
    local mod="$1" ice="$2"

    local -a group1 group2 group3
    reply=()

    # First Group
    : -zui_glib_rc_button "button17" "·New feature·" \
        'reply=( ",all," ); new_feature'
    group1=( $reply ) reply=()

    # Content
    reply=( "$group1[@]" )

    # Non-selectable lines  Hops to jump with [ and ]   Local anchors
    reply2=( )              reply3=( 3 )                reply4=( )

}

generator_B(){
    local -a gener=(generator_B_tree generator_B_preview)
    $gener[1+(ZUI[gen_B_idx]%2)]
}

generator_B_preview() {
    local QPATH=$__list[ZUI[CURRENT_IDX]] QTXT
    ZUI[QPATH]=$QPATH ZUI[COLORING_PATTERN]="·[^·]##·"
    reply=() qlines=()
    if [[ -n $QPATH ]];then
        QTXT="Contents of file: ·$QPATH·"\
" located at: ·${${${(M)QPATH##/*}:+$QPATH:h}:-$PWD}/·"
        qlines=("${(@f)"$(<$QPATH)"}")
    else
        QTXT="!NO FILE SELECTED, PRESS F2 TO SHOW FILE TREE!"
    fi
    -zui_glib_special_text $QTXT
    separ
    reply=( "${reply[1]}" $REPLY "$qlines[@]" )
    reply2=( 1 2 )  reply3=( 1 $GEN2_HOPS )  reply4=( )
}

generator_B_tree() {
    ZUI[COLORING_PATTERN]="(((([^\/—])(#c2,))(#e))|·[^·]##·)"
    ZUI[QPATH]= ZUI[QIDX]=0
    reply=()
    -zui_glib_special_text "Showing files in: ·$PWD/·"
    qfiles=()
    separ
    qfiles+=(**/*~*~(.N))
    reply=( "$reply[1]" $REPLY $qfiles[@] )
    reply2=( 1 2 )  reply3=( 1 $GEN2_HOPS )  reply4=( )
}

# Read & publish configure/make output
-zui-standard-timeout-callback() {
    ((ZUI[VIEW_CHANGE]))||return 0
    integer IN=ZUI[PREV_CURRENT_IDX]

    # Preview
    if ((ZUI[gen_B_idx]%2));then
        ((ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_MAIN_BKP]))&&\
            ZUI[PREV_CURRENT_IDX]=$ZUI[CURRENT_IDX_PVIEW_BKP]
    # Tree
    else
        ((ZUI[PREV_CURRENT_IDX]==ZUI[CURRENT_IDX_PVIEW_BKP]))&&\
            ZUI[PREV_CURRENT_IDX]=$ZUI[CURRENT_IDX_MAIN_BKP]
    fi
    if ((IN!=ZUI[PREV_CURRENT_IDX]));then
        ZUI[CURRENT_IDX]=$ZUI[PREV_CURRENT_IDX]
        -zui_list_verify_vars
        ZUI[redraw]=1
        ZUI[VIEW_CHANGE]=0
    fi
    return 0
}

if (($+Opts[--refresh])); then
    iqw "Reload went OK, status normal"
    return 3
fi

## Start application ##
-zui_glib_msg "" "@@@ " "Welcome to " "IQ System " "open console!" " @@@";
integer EC
while (( 1 )); do
    command tput smcup
    zui-event-loop 1:generator_A 1:generator_B
    command tput rmcup
    [[ -z $REPLY ]]&&break
    local QFILE=; integer QIDX=0
    print  $REPLY >>/tmp/reply
    if ((ZUI[gen_B_idx]%2));then
        QFILE=$ZUI[QPATH]
        QIDX=ZUI[USER_CURRENT_IDX]
    elif [[ $REPLY != *(·|–|—)* ]];then
        QFILE=$REPLY
    fi

    if [[ -n $QFILE ]];then
        if ! int/iq::get-editor;then
            EC+=$?
            -zui_glib_msg "" "" "" 'ERROR: Couldn''t find editor, tried '\
                "\$VISUAL (=$VISUAL) " then "" " \$EDITOR (=$EDITOR)"
            ZUI[SEARCH_MODE]=0
        else
            (
                # Run the editor in $REPLY, first cd-ing into project's directory
                int/iq::run-command -C $PWD \
                    "$REPLY" ${${QIDX:#0}:++$QIDX} -- $QFILE
            )
        fi
        EC+=$?
    fi
    zcurses clear main redraw
    zcurses clear status redraw
done

-zui_glib_cleanup serialize
command tput rmcup
return 0

}

iqopen "$@"

# vim:ft=zsh:tw=80:sw=4:sts=4:et:foldmarker=[[[,]]]
