#!/usr/bin/env zsh
# -*- mode: sh;sh-indentation: 4;indent-tabs-mode: nil;sh-basic-offset: 4; -*-
#
# Copyright (c) 2023 Sebastian Gniazdowski
#

iq::read-theme() {

# Set options
builtin emulate -L zsh -o extendedglob -o noshortloops \
                        -o warncreateglobal -o typesetsilent \
                        -o nopromptsubst -o rcquotes

# Preamble – emulate & 0=…
eval "${SNIP_EMULATE_OPTIONS_ZERO:-false}"||\
    0=${${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}:A}

# The color theme → customize via theme file or ZIQ global, first testing it
(
    {local -A ZIQ=("${(kv@)ZIQ}")} &>>!$ZIQLOG
    return 3
)||\
    (($?==3))&&local -A ZIQ=("${(kv@)ZIQ}")||local -A ZIQ

local ZIQTHEME=$ZIQTHEME
if [[ -f $ZIQDIR/themes/$ZIQTHEME:t:r.xzt(#qN) || -f ${ZIQTHEME::=$ZIQDIR/themes/default.xzt} ]]; then
    local -a q=($ZIQDIR/themes/$ZIQTHEME(.xzt|)(N) $ZIQTHEME(.xzt|)(N))
    eval "ZIQ+=(${(qqq@)${(@z)"$(<$q[1])"}})" &>>!$ZIQLOG ||\
        builtin print -u2 -P -r ${${(%):-%x}:t}:%I: Couldn\'t read theme \
            file: %F{41}$ZIQTHEME%f
else
     builtin print -u2 -P -r ${${(%):-%x}:t}:%I: Couldn\'t read default \
        theme \(%F{41}$ZIQTHEME%f\)
fi
# Return theme
reply=("${(kv@)ZIQ}")
return !$#reply
}
# vim:ft=zsh:sw=4:sts=4:et
