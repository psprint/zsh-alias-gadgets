#!/usr/bin/env zsh

int/iq::get-file-type() {
    # Preamble – emulate & 0=…
    eval "${SNIP_EMULATE_OPTIONS_ZERO:-false}"||\
        0=${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}

    local -a opts; local -A Opts
    int/iq::parse-opts "s -scan" Opts opts "$@"||return 7
    set -- "$reply[@]"
    int/iq::opt-cascade Opts -s --scan
    int/iq::reset
    integer EC
    local QPATH=$1
    local -a qc

    [[ $QPATH == WRONGSTR ]]&&{iqerr incorrect or missing path to file;return 13;}
    [[ ! -r $QPATH ]]&&unset 'Opts[-s]' 'Opts[--scan]'

    if (($+Opts[-s]));then
        qc=(${(f)"$(<$QPATH)"})
        qc=(${qc/((#s)?(#c0,2)[[:space:]]#vim*(:|)(ft|filetype)=(#b)([^:[:space:]]##)(#B)*|(#s)?(#c0,2)[[:space:]]#"-*- mode:"[[:space:]]#(#b)([^\;]##)(#B)*|*)/$match[1]$match[2]})
        reply=($qc $QPATH:e)
        reply=(${(uon)reply})
        REPLY=$reply[1]
    else
        REPLY=$QPATH:e reply=($QPATH:e)
    fi
 
    if [[ $REPLY == WRONGSTR ]]; then
        iqwarn -Q ${Opts[-q]+-q} $ZIQ[Q27_FILETYPE_UNRECOGNIZED]
        return 1
    fi
    return 0
}
