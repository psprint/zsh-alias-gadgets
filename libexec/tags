#!/usr/bin/env zsh
# Copyright (c) 2023 Sebastian Gniazdowski
#
# iqsn – zs…quote…braces
#

tags() {

# Run as script?
[[ $0 != tags ]]&&integer bescript=1

# Set options
builtin emulate -L zsh -o extendedglob -o noshortloops \
                        -o warncreateglobal -o typesetsilent \
                        -o nopromptsubst

# Set $0 with a new trik - use of %x prompt expansion
0=${${(M)${0::=${(%):-%x}}:#/*}:-$PWD/$0}
source $0:h:h/share/preamble.inc.zsh --func

local -A Opts
builtin zparseopts \
    ${${(M)ZSH_VERSION:#(5.[8-9]|6.[0-9])}:+-F} \
    -D -E -A Opts -- z -zsh c -c v -verbose||return 7
# Cascade options to short ones
local Q P
for Q P in --zsh -z --c -c;do
    (($+Opts[$Q]))&&Opts[$P]=
done
# Info texts
local TXT
(($+Opts[-z]))&&TXT="{type}Zsh{%} and {type}Make{%}"
(($+Opts[-c]))&&TXT="{type}C{%} and {type}Make{%}"
# Commands
if (($+Opts[-c]));then
    local -a qcmd=(command ctags -R -e --totals
                  --languages=C --langmap=C:.h.c
                  --c-kinds=+px --extras=+r
                  --extras=-{anonymous})
else 
    local -a qcmd=(command ctags -R -e -G --totals
        --options=$ZIQDIR/share/zsh.ctags
        --languages=zsh,zsh3,make)
fi

local TARGET_DIR=${1:-$PWD}

(
    builtin cd -q -- $TARGET_DIR||\
        {
            iqerr no such directory {path}$TARGET_DIR
            return 1
        }
    iqmsg {note}Generating {tag}TAGS{%} index for $TXT…
    (($+Opts[-v]))&&iqnotice -- Running: {cmd}$qcmd[*]{%}…
    "$qcmd[@]"
)||return $?

}
